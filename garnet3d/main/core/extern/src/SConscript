Import( 'env GN_targets GN_func' )

env = env.Copy()
if '/W4' in env['CCFLAGS']: env['CCFLAGS'].remove('/W4')
if 'icl' == env['CC']:
    unimportant_warnings = [
        '1572', # floating-point comparison
        ]
    env.Append( CCFLAGS = ['/W3'] + ['/Qwd%s'%w for w in unimportant_warnings] )
elif 'cl' == env['CC']:
    env.Append( CCFLAGS= ['/W3'] )

env.Append( CPPPATH = '../inc/garnet/extern' )

# compile jpeg library
jpeg_env = env.Copy()
jpeg_env.Append( CPPPATH = [Dir('jpeg').srcnode()] )
if 'gcc' == env['CC']:
    jpeg_env.Append( CCFLAGS = ['-Wno-main'] )
jpeg_objs = GN_func['build_static_objects'](
    jpeg_env,
    GN_func['glob']( '*.c', 'jpeg' ),
    pdb = 'GnExtern.pdb'
    )

# build source list
srcs = GN_func['glob']( '*.c', 'glew' ) \
     + GN_func['glob']( '*.c', 'lua' ) \
     + GN_func['glob']( '*.cpp', 'tinyxml' ) \
     + GN_func['glob']( '*.c', 'zlib' ) \
     + GN_func['glob']( '*.c', 'png' ) \
     + jpeg_objs

GN_targets['GnExtern'] = GN_func['build_static_library'](
    env,
    'GnExtern',
    srcs,
    pdb = 'GnExtern.pdb' )
