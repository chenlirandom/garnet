Import( 'env GN_conf' )

import sys, glob

# initialize local environment
env = env.Copy()
env.Append( CPPPATH = ['.'] )
if GN_conf['has_boost']: env.Append( CPPDEFINES = ['HAS_BOOST'] )

# build case list
cases = []
cases += env.GN_glob( 'base/*.cpp' )
cases += env.GN_glob( 'gfx/lib/*.cpp' )
if 'win32' == env['PLATFORM']: cases += ['gfx/renderer/ogl.cpp']
if 'win32' == env['PLATFORM']: cases += ['gfx/renderer/d3d.cpp']

# generate cxxtest glue file.
cxxtest = env.Command(
    'ut.cpp', cases,
    '%s %s --error-printer --include="garnet/GNbase.h" -o $TARGET ${SOURCES.abspath}'%(
        sys.executable,
        File('cxxtestgen.py').srcnode().path)
    )
env.Depends( cxxtest, 'cxxtestgen.py' )

# build unit test application
env.GN_build_program( 'GNut', ['testCommon.cpp',cxxtest] )
