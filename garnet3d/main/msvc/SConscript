Import('env GN_targets GN_conf')

import os.path

#
#
# -----------------------------------------------------------------------------
def to_relpath( files ):
    return [ env.GN_relpath( File(x).path, Dir('.').srcnode().path ) for x in files ]

#
#
# -----------------------------------------------------------------------------
def build_proj( target, srcdir=[], incdir=[] ):
    if not target in GN_targets: return

    sources = env.GN_glob( '*.[c]*', srcdir, True )
    if 0 == len(incdir):
        includes = env.GN_glob( '%s.h'%target, '#src/priv/inc/garnet' )
        includes += env.GN_glob( '*.[hi]*', ['#src/priv/inc/garnet/%s'%target[2:]], True )
    else:
        includes = env.GN_glob( '*.[hi]*', incdir, True )
    localincs = env.GN_glob( '*.[hi]*', srcdir, True )

    proj = env.MSVSProject(
        target      = target + env['MSVSPROJECTSUFFIX'],
        buildtarget = GN_targets[target],
        variant     = GN_conf['variant'],
        srcs        = to_relpath(sources),
        incs        = to_relpath(includes),
        localincs   = to_relpath(localincs),
        resources   = [],
        misc        = [],
        )

    env.AlwaysBuild( proj );

###############################################################################
#
# Build project for garnet components
#
###############################################################################

build_proj( 'GNextern', ['#src/extern/src'], ['#src/extern/inc'] )
build_proj( 'GNbase', ['#src/priv/base'] )
build_proj( 'GNcore', ['#src/priv/core'] )
build_proj( 'GNgfxCommon', ['#src/priv/gfx/common'] )
build_proj( 'GNgfxD3D', ['#src/priv/gfx/d3d'] )
build_proj( 'GNgfxOGL', ['#src/priv/gfx/ogl'] )
build_proj( 'GNgfxTest', ['#src/priv/gfx/test'] )
build_proj( 'GNd3d', ['#src/priv/d3d'] )
build_proj( 'GNd3dTest', ['#src/priv/d3d/test'] )
build_proj( 'GNogl', ['#src/priv/ogl'] )
build_proj( 'GNoglTest', ['#src/priv/ogl/test'] )
build_proj( 'GNtest', ['#src/priv/test'] )
