Import( 'env GN_conf GN_targets GN_func' )

import sys, glob

# initialize local environment
env = env.Copy()
env.Append( CPPPATH = ['.'] )
if GN_conf['has_boost']: env.Append( CPPDEFINES = ['HAS_BOOST'] )

# generate cxxtest glue file.
cases   = GN_func['glob']('*.cpp', 'base')
cxxtest = env.Command(
    'ut.cpp', cases,
    '%s %s --error-printer --include="garnet/GnBase.h" -o $TARGET ${SOURCES.abspath}'%(
        sys.executable,
        File('cxxtestgen.py').srcnode().path)
    )

# build unit test application
GN_targets['GnTest'] = GN_func["build_program"]( env, 'GnTest', ['testCommon.cpp',cxxtest] )

# run unit test
def run_test( target, source, env ):
    import os
    return os.spawnl( os.P_WAIT, target[0].path )
AddPostAction( GN_targets['GnTest'][0], run_test )
