Import( 'env GN_conf GN_targets' )

import os, os.path

variant_dir = os.path.join( GN_conf['platform'], GN_conf['compiler'], GN_conf['variant'] )

def doInstall( alias, dir, names ):
    for name in names:
        if name in GN_targets and GN_targets[name]:
            item = env.Install( os.path.join(dir, variant_dir), GN_targets[name] )
            GN_targets[name] = item
            env.Alias( alias, item )

# populate sample directory
doInstall(
    'samples',
    'samples',
    Split('GNcoreBin GNgfxD3DBin GNgfxOGLBin GNtest GNgfxTest') )

# make test executable depends on all shared libraries.
for y in Split('GNtest GNgfxTest'):
    if y in GN_targets:
        for x in Split('GNcoreBin GNgfxD3DBin GNgfxOGLBin'):
            if x in GN_targets: env.Depends( GN_targets[y], GN_targets[x] )
    else:
        env.GN_warn( "Target '%s' is not avaliable!"%y )

# Make binaries depend on manifest and PDB files, to make sure those files are copied
# to binary directory, before execution of the binaries.
for target in Split('GNcoreBin GNgfxD3DBin GNgfxOGLBin GNtest GNgfxTest'):
    if target in GN_targets:
        for x in GN_targets[target][1:]:
            env.Depends( GN_targets[target][0], x )
    else:
        env.GN_warn( "Target '%s' is not avaliable!"%target )

# run unit test
if GN_conf['run_unit_tests']:
    def run_test( target, source, env ):
        return os.spawnl( os.P_WAIT, target[0].path )
    AddPostAction( GN_targets['GNtest'][0], run_test )

# populate SDK directory
doInstall(
    'sdk',
    os.path.join('sdk','bin'),
    Split('GNcoreBin GNgfxD3DBin GNgfxOGLBin') )
doInstall(
    'sdk',
    os.path.join('sdk','lib'),
    Split('GNcoreLib GNgfxD3DLib GNgfxOGLLib GNbase GNextern GNd3d GNogl') )
